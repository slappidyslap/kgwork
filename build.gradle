import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.9'
	id 'io.spring.dependency-management' version '1.1.2'
	id "co.uzzu.dotenv.gradle" version "2.0.0"
	id 'org.liquibase.gradle' version '2.2.0'
}

group = 'kg.musabaev'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	mapstructVersion = "1.5.5.Final"
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.liquibase:liquibase-core'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly "org.mapstruct:mapstruct:$mapstructVersion"
	annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/changelog.yml'
			url env.DB_URL.value
			username env.DB_USERNAME.value
			password env.DB_PASSWORD.value
		}
	}
}

tasks.register('bootDevRun', BootRun) {
	mainClass = bootRun.mainClass
	classpath = bootRun.classpath
	systemProperty "spring.profiles.active", "dev"
}

tasks.withType(BootRun).configureEach {
	environment "DB_URL", env.DB_URL.value
	environment "DB_USERNAME", env.DB_USERNAME.value
	environment "DB_PASSWORD", env.DB_PASSWORD.value
}

tasks.named('test') {
	useJUnitPlatform()
}
